buildscript {
    ext {
        kotlinVersion = "1.3.30"
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.30"
    id "idea"
}

sourceSets {
    main.kotlin.srcDirs += 'src/main'
}

idea {
    module {
        excludeDirs += file('robocode')
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'net.sf.robocode', name: 'robocode.api', version: '1.9.2.1'
    runtime fileTree(include: ['*.jar'], dir: "${robocodeDir}/libs")
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

task install {
    doLast {
        def packagePath = 'as/ter'

        def fromPath = "${rootDir}/build/classes/kotlin/main/${packagePath}"
        def intoPath = "${robocodeDir}/robots/${packagePath}"

        copy {
            from fromPath
            into intoPath

            eachFile { details ->
                println "Copied ${details}"
            }
        }
    }
}

tasks.register('start') {
    group = 'robocode'

    doLast {
        exec {
            commandLine 'java',
                    "-Xmx512M",
                    "-DWORKINGDIRECTORY=${robocodeDir}",
                    "-cp", "${robocodeDir}/libs/robocode.jar", "robocode.Robocode",
                    '-battle', "${rootDir}/battles/nomovement.battle"
        }
    }
}

tasks.install.dependsOn build
tasks.start.dependsOn install
