buildscript {
    ext {
        kotlinVersion = "1.3.30"
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.30"
    id "idea"
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
}

idea {
    module {
        excludeDirs += file('robocode')
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation fileTree(include: ['*.jar'], dir: "${robocodeDir}/libs")
}

jar {
    manifest {
        attributes "robots": "as.ter.TerasBot"
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task install {
    doLast {
        def fromPath = "${rootDir}/build/libs/terasbot-robocode.jar"
        def intoPath = "${robocodeDir}/robots/"

        copy {
            from fromPath
            into intoPath

            eachFile { details ->
                println "Copied ${details}"
            }
        }
    }
}

tasks.register('start') {
    group = 'robocode'

    doLast {
        exec {
            commandLine 'java',
                    "-Xmx512M",
                    "-Ddebug=true",
                    "-DWORKINGDIRECTORY=${robocodeDir}",
                    "-cp", "${robocodeDir}/libs/robocode.jar", "robocode.Robocode",
                    '-battle', "${rootDir}/battles/nomovement.battle"
        }
    }
}

tasks.install.dependsOn jar
tasks.start.dependsOn install
